package miniJava.parser;

import java_cup.runtime.*;
import java.io.IOException;
import java.nio.file.Path;
import miniJava.astree.*;
import miniJava.astree.declaration.classDeclaration.*;
import miniJava.astree.declaration.methodDeclaration.*;
import miniJava.astree.declaration.variableDeclaration.*;
import miniJava.astree.expression.*;
import miniJava.astree.expression.array.*;
import miniJava.astree.expression.bool.*;
import miniJava.astree.expression.identifier.*;
import miniJava.astree.expression.integer.*;
import miniJava.astree.program.*;
import miniJava.astree.statement.*;
import miniJava.astree.type.*;
import miniJava.astree.type.nonPrimitive.*;
import miniJava.astree.type.primitive.*;
import miniJava.lexer.Lexer;

parser code {:
    private Lexer _Lexer;

    private Path filePath;

    public Parser(Path filePath) throws IOException {
        this.filePath = filePath;

        ComplexSymbolFactory cSymFac = new ComplexSymbolFactory();

        super.symbolFactory = symbolFactory;

        _Lexer = new Lexer(filePath, cSymFac);
    }
:}

init with {: :};

scan with {:
    return _Lexer.next_token();
:};

// Terminais sem valores associados
terminal CLASS, PUBLIC, EXTENDS, STATIC, VOID, INT, BOOL, WHILE, IF, ELSE, RETURN, THIS, NEW,
         SEMICOLON, DOT, COMMA, ASS, O_PAR, C_PAR, O_BRACE, C_BRACE, O_BRACKET, C_BRACKET,
         OR, AND, EQUAL, N_EQUAL, LESS, L_EQUAL, GREATER, G_EQUAL, PLUS, MINUS, TIMES, DIV, MOD, NEG;

terminal MAIN, STRING, SYSO, LENGTH;

// Terminais com valores associados
terminal Boolean FALSE, TRUE;
terminal Integer INTEGER;
terminal String ID;

// Nao terminais
non terminal Program program;
non terminal MainClassDeclaration mainClass;
non terminal ClassListDeclaration classList;
non terminal IClassDeclaration classDeclaration;
non terminal VariableListDeclaration varList;
non terminal VariableDeclaration varDeclaration;
non terminal MethodListDeclaration methodList;
non terminal MethodDeclaration methodDeclaration;
non terminal IDataType dataType;
non terminal ArgListDeclaration argList;
non terminal ArgListDeclaration args;
non terminal ArgDeclaration argDeclaration;
non terminal String ext;
non terminal StatementList stmtList;
non terminal IStatement stmt;
non terminal ExpressionList paramList;
non terminal ExpressionList paramDeclaration;
non terminal IExpression expr;

/* Regras de precedencia

    ex.: (ASSIGNEE) a = b = c
    eh associativo a direita,
    fazendo que a expressao mais a direita
    seja avaliada primeiro,
    podendo alimentar varias variaveis com o mesmo valor
*/

precedence right ASS;

precedence left AND, OR;

precedence right NEG;

precedence nonassoc EQUAL, N_EQUAL, LESS, L_EQUAL, GREATER, G_EQUAL;

precedence left PLUS, MINUS;
precedence left TIMES, DIV, MOD;

precedence nonassoc ID;

precedence left DOT;

precedence right NEW;

// Gramatica
program             ::= mainClass:mc classList:cl {: RESULT = new Program(mc, cl); :}
                    ;

mainClass           ::= CLASS ID:cId O_BRACE PUBLIC STATIC VOID MAIN O_PAR STRING O_BRACKET C_BRACKET ID C_PAR O_BRACE stmtList:stmts C_BRACE C_BRACE {: RESULT = new MainClassDeclaration(cId, stmts); :}
                    ;

classList           ::= classList:cl classDeclaration:cd    {: cl.add(cd); RESULT = cl; :}
                        |                                   {: RESULT = new ClassListDeclaration(); :}
                    ;

classDeclaration    ::= CLASS ID:cId ext:ext O_BRACE varList:vl methodList:ml C_BRACE {: if (ext == null) RESULT = new SimpleClassDeclaration(cId, vl, ml); else RESULT = new ExtendedClassDeclaration(cId, vl, ml, ext); :}
                    ;

ext                 ::= EXTENDS ID:id   {: RESULT = id; :}
                        |               {: RESULT = null; :}
                    ;

varList             ::= varList:vl varDeclaration:vd    {: vl.add(vd); RESULT = vl; :}
                        |                               {: RESULT = new VariableListDeclaration(); :}
                    ;

varDeclaration      ::= dataType:dt ID:id SEMICOLON   {: RESULT = new VariableDeclaration(dt, id); :}
                    ;

methodList          ::= methodList:ml methodDeclaration:md  {: ml.add(md); RESULT = ml; :}
                        |                                   {: RESULT = new MethodListDeclaration(); :}
                    ;

methodDeclaration   ::= PUBLIC dataType:dt ID:mId O_PAR argList:pl C_PAR O_BRACE varList:vl stmtList:sl RETURN expr:expr SEMICOLON C_BRACE    {: RESULT = new MethodDeclaration(dt, mId, pl, vl, sl, expr); :}
                    ;

dataType            ::= INT                         {: RESULT = new IntType(); :}
                        | INT O_BRACKET C_BRACKET   {: RESULT = new IntArrayType(); :}
                        | BOOL                      {: RESULT = new BoolType(); :}
                        | ID:name                   {: RESULT = new IdType(name); :}
                    ;

argList           ::= args:p    {: RESULT = p; :}
                        |           {: RESULT = new ArgListDeclaration(); :}
                    ;

args              ::= argDeclaration:pd             {: RESULT = new ArgListDeclaration(); RESULT.add(pd); :}
                        | args:pd COMMA argDeclaration:p {: pd.add(p); RESULT = pd; :}
                    ;

argDeclaration    ::= dataType:dt ID:id   {: RESULT = new ArgDeclaration(dt, id); :}
                    ;

stmtList            ::= stmt:s stmtList:sl {: sl.add(s); RESULT = sl; :}
                        |                   {: RESULT = new StatementList(); :}
                    ;

stmt                ::= O_BRACE stmtList:sl C_BRACE                                     {: RESULT = new BraceStatement(sl); :}
                        | IF O_PAR expr:expr C_PAR stmt:ifStmt ELSE stmt:elseStmt       {: RESULT = new IfElseStatement(expr, ifStmt, elseStmt); :}
                        | WHILE O_PAR expr:expr C_PAR stmt:stmt                         {: RESULT = new WhileStatement(expr, stmt); :}
                        | SYSO O_PAR expr:expr C_PAR SEMICOLON                          {: RESULT = new SysoStatement(expr); :}
                        | ID:id ASS expr:expr SEMICOLON                                 {: RESULT = new AssStatement(id, expr); :}
                        | ID:id O_BRACKET expr:expr C_BRACKET ASS expr:assExpr SEMICOLON  {: RESULT = new ArrayAssStatement(id, expr, assExpr); :}
                    ;

expr                ::= expr:l AND expr:r                           {: RESULT = new BinaryBooleanExpression(l, BinaryBooleanExpression.Operator.AND, r); :}
                        | expr:l OR expr:r                          {: RESULT = new BinaryBooleanExpression(l, BinaryBooleanExpression.Operator.OR, r); :}
                        | expr:l EQUAL expr:r                       {: RESULT = new BinaryBooleanExpression(l, BinaryBooleanExpression.Operator.EQUAL, r); :}
                        | expr:l N_EQUAL expr:r                     {: RESULT = new BinaryBooleanExpression(l, BinaryBooleanExpression.Operator.N_EQUAL, r); :}
                        | expr:l LESS expr:r                        {: RESULT = new BinaryBooleanExpression(l, BinaryBooleanExpression.Operator.LESS, r); :}
                        | expr:l L_EQUAL expr:r                     {: RESULT = new BinaryBooleanExpression(l, BinaryBooleanExpression.Operator.L_EQUAL, r); :}
                        | expr:l GREATER expr:r                     {: RESULT = new BinaryBooleanExpression(l, BinaryBooleanExpression.Operator.GREATER, r); :}
                        | expr:l G_EQUAL expr:r                     {: RESULT = new BinaryBooleanExpression(l, BinaryBooleanExpression.Operator.G_EQUAL, r); :}
                        | expr:l PLUS expr:r                        {: RESULT = new BinaryIntegerExpression(l, BinaryIntegerExpression.Operator.PLUS, r); :}
                        | expr:l MINUS expr:r                       {: RESULT = new BinaryIntegerExpression(l, BinaryIntegerExpression.Operator.MINUS, r); :}
                        | expr:l TIMES expr:r                       {: RESULT = new BinaryIntegerExpression(l, BinaryIntegerExpression.Operator.TIMES, r); :}
                        | expr:l DIV expr:r                       {: RESULT = new BinaryIntegerExpression(l, BinaryIntegerExpression.Operator.DIV, r); :}
                        | expr:l MOD expr:r                         {: RESULT = new BinaryIntegerExpression(l, BinaryIntegerExpression.Operator.MOD, r); :}
                        | NEG expr:e                                {: RESULT = new UnaryBooleanExpression(UnaryBooleanExpression.Operator.NEG, e); :}
                        | expr:a O_BRACKET expr:i C_BRACKET         {: RESULT = new ArrayAccessExpression(a, i); :}
                        | expr:a DOT LENGTH                         {: RESULT = new LengthExpression(a); :}
                        | expr:o DOT ID:m O_PAR paramList:al C_PAR    {: RESULT = new MethodCallExpression(o, m, al); :}
                        | INTEGER:i                                 {: RESULT = new LiteralIntegerExpression(i); :}
                        | FALSE:f                                   {: RESULT = new LiteralBooleanExpression(f); :}
                        | TRUE:t                                    {: RESULT = new LiteralBooleanExpression(t); :}
                        | ID:id                                     {: RESULT = new IdentifierExpression(id); :}
                        | THIS                                      {: RESULT = new ThisExpression(); :}
                        | NEW INT O_BRACKET expr:expr C_BRACKET     {: RESULT = new ArrayCreationExpression(expr); :}
                        | NEW ID:id O_PAR C_PAR                     {: RESULT = new ObjectCreationExpression(id); :}
                        | O_PAR expr:expr C_PAR                     {: RESULT = expr; :}
                    ;

paramList             ::= paramDeclaration:ad   {: RESULT = ad; :}
                        |                   {: RESULT = new ExpressionList(); :}
                    ;

paramDeclaration      ::= expr:expr COMMA paramDeclaration:params   {: params.add(expr); RESULT = params; :}
                        | expr:expr                               {: RESULT = new ExpressionList(); RESULT.add(expr); :}
                    ;